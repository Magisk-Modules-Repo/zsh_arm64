#compdef notmuch

_notmuch_commands()
{
  local -a notmuch_commands
  notmuch_commands=(
    'setup:interactively set up notmuch for first use'
    'new:find and import any new message to the database'
    'insert:add a new message into the maildir and notmuch database'
    'search:search for messages matching the search terms, display matching threads as results'
    'address:get addresses from messages matching the given search terms'
    'reply:constructs a reply template for a set of messages'
    'show:show all messages matching the search terms'
    'tag:add or remove tags for all messages matching the search terms'
    'dump:creates a plain-text dump of the tags of each message'
    'restore:restores the tags from the given file'
    'compact:compact the notmuch database'
    'reindex:re-index all messages matching the search terms'
    'help:show details on a command'
    'compact:compact the notmuch database'
    'config:access the notmuch configuration file'
    'count:count messages matching the given search terms'
  )

  _describe -t command 'command' notmuch_commands
}

_notmuch_dump()
{
  _files
}

_notmuch_help_topics()
{
  local -a notmuch_help_topics
  notmuch_help_topics=(
    'search-terms:common search-term syntax'
    'hooks:hooks that will be run before or after certain commands'
    'properties:message property conventions and documentation'
  )
  _describe -t notmuch-help-topics 'topic' notmuch_help_topics
}

_notmuch_help()
{
  _alternative \
    _notmuch_commands \
    _notmuch_help_topics
}

_notmuch_restore()
{
  _files
}

_notmuch_search()
{
  _arguments -s : \
    '--max-threads=[display only the first x threads from the search results]:number of threads to show: ' \
    '--first=[omit the first x threads from the search results]:number of threads to omit: ' \
    '--sort=[sort results]:sorting:((newest-first\:"reverse chronological order" oldest-first\:"chronological order"))'
}

_notmuch()
{
  local ret=1
  if (( CURRENT > 2 )) ; then
    local cmd=${words[2]}
    curcontext="${curcontext%:*:*}:notmuch-$cmd"
    (( CURRENT-- ))
    shift words
    if ! _call_function ret _notmuch_$cmd; then
      _default && ret=0
    fi
    return ret
  else
    _notmuch_commands
  fi
}

_notmuch "$@"
